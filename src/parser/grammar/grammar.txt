1. program -> declaration_list
2. declaration_list -> declaration_list declaration | declaration
3. declaration -> var_declaration | fun_declaration
4. var_declaration -> type_specifier PID ID VAR_DEC ; | type_specifier PID ID [ PSIZE NUM ] ARRAY_DEC ;
5. type_specifier -> PTYPE int | void
6. fun_declaration -> type_specifier PID ID FUNC ( params ) compound_stmt
7. params -> param_list | void
8. param_list -> param_list , param | param
9. param -> type_specifier PID ID | type_specifier PID ID [ ]
10. compound_stmt -> { local_declarations statement_list }
11. local_declarations -> local_declarations var_declaration | Epsilon
12. statement_list -> statement_list statement | Epsilon
13. statement -> expression_stmt | compound_stmt | selection_stmt | iteration_stmt | return_stmt | switch_stmt
14. expression_stmt -> expression ; | break BREAK_JP ; | ;
15. selection_stmt -> if ( expression ) SAVE statement endif JPF | if ( expression ) SAVE statement JPF_SAVE else statement endif JP
16. iteration_stmt -> while LABEL_WHILE ( expression ) SAVE statement WHILE
17. return_stmt -> return ; | return expression ;
18. switch_stmt -> switch LABEL_SWITCH ( expression ) { case_stmts default_stmt }
19. case_stmts -> case_stmts case_stmt | Epsilon
20. case_stmt -> case PNUM NUM SAVE : statement_list JPF
21. default_stmt -> default : statement_list | Epsilon
22. expression -> var = expression ASSIGN | simple_expression
23. var -> PID ID | PID ID [ expression ] ARRAY_CELL
24. simple_expression -> additive_expression relop additive_expression RELOP | additive_expression
25. relop -> P_OP < | P_OP ==
26. additive_expression -> additive_expression addop term ADDOP | term
27. addop -> P_OP + | P_OP -
28. term -> term mulop factor MULTOP | factor
29. mulop -> P_OP * | P_OP /
30. factor -> ( expression ) | var | call | PNUM NUM | call_output
31. call -> PID ID ( args )
xx. call_output -> output ( args ) OUTPUT
32. args -> arg_list | Epsilon
33. arg_list -> arg_list , expression | expression
34. PID -> Epsilon
35. PNUM -> Epsilon
36. PTYPE -> Epsilon
37. FUNC -> Epsilon
38. VAR_DEC -> Epsilon
39. ARRAY_DEC -> Epsilon
40. BREAK_JP -> Epsilon
41. SAVE -> Epsilon
42. JPF -> Epsilon
43. JPF_SAVE -> Epsilon
44. JP -> Epsilon
45. WHILE -> Epsilon
46. LABEL_WHILE -> Epsilon
47. LABEL_SWITCH -> Epsilon
48. ASSIGN -> Epsilon
49. ARRAY_CELL -> Epsilon
50. RELOP -> Epsilon
51. ADDOP -> Epsilon
52. MULTOP -> Epsilon

Keywords: else if int return void while switch case default break endif

Special symbols: + - * / < == = : ; , ( ) [ ] { } /* */ //

ID = letter letter*
NUM = digit digit*
letter = a | .. | z | A | .. | Z
digit = 0 | .. | 9

Comments: /* ... */

